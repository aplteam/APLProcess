 R←Test_Misc_040(stopFlag batchFlag);⎕IO;⎕ML;⎕TRAP;parms;tempFilename;P;okayFlag;expected;content;remoteVersion;counter
⍝ Start a process with the oldest supported version of Dyalog.
⍝ What's oldest version is established from the Make/make.bat file in the current directory.
⍝ Which version was loaded is established by analyzing the log file which is written by the
⍝ "Version" workspace to the file specified by the "file=" parameter.
⍝ See also Test_Misc_050 which checks the same but with a very different approach.
 ⎕IO←1 ⋄ ⎕ML←1
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←T._Failed

 parms←##.APLProcess.CreateParms
 parms.WorkspaceName←##.FilesAndDirs.PWD,'\Tests\Version.dws'
 parms.Exe←1↓{⍵/⍨2>+\'"'=⍵}1⊃##.APLTreeUtils.ReadUtf8File'Make/Make.bat'
 tempFilename←##.FilesAndDirs.GetTempFilename2''
 parms.CommandLineArgs←'file="',tempFilename,'"'
 parms.LogFilename←##.FilesAndDirs.GetTempFilename2'' ⍝ In order to get a fresh LOG
 :If 0
     parms.SessionFile←1
     parms.CommandLineArgs,←' -x'  ⍝ For debugging the workspace that will be run
 :EndIf
 P←⎕NEW ##.APLProcess parms
 okayFlag←counter←0

 :Repeat
     :If ~##.FilesAndDirs.IsFile parms.LogFilename
     :OrIf 0=≢content←##.APLTreeUtils.ReadUtf8File tempFilename
         ⎕DL 0.5
     :Else
         expected←{⍵↑⍨1+-⌊/(⌽⍵)⍳'/\'}¯1↓1⊃⎕NPARTS parms.Exe
         expected←⊃(//)⎕VFI 3⊃' '##.APLTreeUtils.Split expected
         remoteVersion←⊃(//)⎕VFI{⍵/⍨2>+\⍵='.'}⊃¯1↑' '##.APLTreeUtils.Split content
         okayFlag←expected=remoteVersion
     :EndIf
 :Until okayFlag∨20<counter←counter+1
 →T.PassesIf okayFlag

 R←T._OK

∆TidyUp:
 ##.FilesAndDirs.DeleteFile tempFilename parms.LogFilename
⍝Done
